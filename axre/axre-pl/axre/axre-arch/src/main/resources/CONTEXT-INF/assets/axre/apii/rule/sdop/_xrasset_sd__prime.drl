//created on: Aug 29, 2015
package assets.axre.rule.apii.sdop

//list any import classes here.

import com.concept.ruleengine.model.EphemeralMantle
import com.concept.ruleengine.common.util.StringUtil
import com.concept.ruleengine.common.util.DateUtil
import com.concept.ruleengine.common.util.CustomUtil

import com.concept.ruleengine.reference.dao.ReferenceDAO
import com.concept.ruleengine.reference.mapper.QueryMapper


global com.concept.ruleengine.reference.dao.ReferenceDAO reference
global com.concept.ruleengine.reference.mapper.QueryMapper mapper

function EphemeralMantle initiation(EphemeralMantle em){
	em.facts.put("timeBuffer", 15);
	
	em.facts.remap("in7", "_type");
	return em;
}

function EphemeralMantle initiationBulkAllocation(EphemeralMantle em,
		QueryMapper mapper, ReferenceDAO reference){

	String eta = DateUtil.toString(
    		StringUtil.toTimestamp(em.facts.get("in1"), 
    			"yyyy-MM-dd'T'HH:mm:ss.SSS'Z'"), "yyyy-MM-dd HH:mm:ss");
		
	em.facts.put("_inEtd", eta);
	
	mapper.relace("_sqlmap_sd","count_bulk_aircraftvisit");
	mapper.parameterized("ETAD",em.facts.get("_inEtd") );
	em.facts.put("bulksize", reference.getInt(mapper.getQuery()));
	
	em.logs("BULK(N) PROCESSING:"+em.facts.get("bulksize"));
	
	em.facts.put("_visitSeq", 0);
	
	em.facts.put("bulkoffset", 0);
	em.facts.put("bulklimit", 10);
	
	return em;
}

rule "initiation-calibration:initiated-phase"
    when
        $em: EphemeralMantle(operationName == "stand-operation",
        	phase == "0000000", 
        		round == 0
    			)
    then
    	$em = initiation($em);
    	$em.setPhase("update-operation-frametime");
    	update ( $em );
end


//declare any global variables here

/* Note from AION,
 * A: Bulk Processes (Day of Operation + 1) (Update Stand + Update Status)
 * B: Move button/ Towing(Check Only, RMS already validate others, No Update Status)
 * C: Conflicts(Update Stand), Resource Allocaiton
 * D: Adhoc(Update Stand + Update Status!) Resource Allocaiton
 * E: Update flight during AODBAFSUPDATE from FMM (Update Status)
 */
 
rule "bulk-allocation:determination"
    when
        $em: EphemeralMantle(operationName == "stand-operation",
        	phase == "initiation-calibration:input-validity", 
        		round == 0, facts.get("_type") == "A", 
        			!CustomUtil.isNullOrBlank(facts.get("in1"))
    			)
    then
    	$em.logs("BULK-ALLOCATION:DETERMINATION");
    	$em = initiationBulkAllocation($em, mapper, reference);
    	
    	$em.facts.put("onfitnessAssignation", true);
    	$em.setPhase("bulk-allocation");
    	update( $em );
end

rule "single-validation:determination"
    when
        $em: EphemeralMantle(operationName == "stand-operation",
        	phase == "initiation-calibration:input-validity", 
        		round == 0, facts.get("_type") == "B", 
        			!CustomUtil.isNullOrBlank(facts.get("in0")),
        			!CustomUtil.isNullOrBlank(facts.get("in3")),
        			!CustomUtil.isNullOrBlank(facts.get("in4")),
        			!CustomUtil.isNullOrBlank(facts.get("in6"))
    			)
    then
    	$em.logs("SINGLE-VALIDATION:DETERMINATION TYPE "+ $em.facts.get("_type"));
    	$em.facts.remap("in0", "_standCode");
		$em.facts.remap("in3","_serviceType");
		$em.facts.remap("in4","_aircraftType");
		$em.facts.remap("in6", "_visitSeq");
		$em.facts.put("onfitnessAssignation", false);
		
		$em.setPhase("single-validation:fitness-stand-initiation");
    	update( $em );
    	
end


rule "single-allocation:determination"
    when
        $em: EphemeralMantle(operationName == "stand-operation",
        	phase == "initiation-calibration:input-validity", 
        		round == 0, facts.get("_type") == "C", 
        			!CustomUtil.isNullOrBlank(facts.get("in6"))
    			)
    then
    	$em.logs("SINGLE-ALLOCATION:DETERMINATION");
    	$em.facts.remap("in6", "_visitSeq");
    	$em.facts.put("onfitnessAssignation", true);
    	
    	$em.setPhase("single-allocation");
    	update( $em );
end

rule "adhoc-allocation:determination"
    when
        $em: EphemeralMantle(operationName == "stand-operation",
        	phase == "initiation-calibration:input-validity", 
        		round == 0, facts.get("_type") == "D", 
        			!CustomUtil.isNullOrBlank(facts.get("in6"))
    			)
    then
    	$em.logs("ADHOC-ALLOCATION:DETERMINATION");
    	$em.facts.remap("in6", "_visitSeq");
    	$em.facts.put("onfitnessAssignation", true);
    	
    	$em.setPhase("single-allocation");
    	update( $em );
end

rule "adhoc-validation:determination"
    when
        $em: EphemeralMantle(operationName == "stand-operation",
        	phase == "initiation-calibration:input-validity", 
        		round == 0, facts.get("_type") == "E", 
        			!CustomUtil.isNullOrBlank(facts.get("in0")),
        			!CustomUtil.isNullOrBlank(facts.get("in5")),
        			!CustomUtil.isNullOrBlank(facts.get("in6"))
    			)
    then
    	$em.logs("ADHOC-VALIDATION:DETERMINATION");
    	$em.facts.remap("in0", "_standCode");
    	$em.facts.remap("in5","_spSeq");
    	$em.facts.remap("in6", "_visitSeq");
    	
    	$em.facts.put("onfitnessAssignation", true);
    	
    	$em.setPhase("single-validation");
    	update( $em );
end
